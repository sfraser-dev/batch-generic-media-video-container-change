@echo off

:: MPEG note:
:: A FFmpeg extension of ".mpg" targets MPEG PS, an extension of ".ts" targets MPEG TS.
:: Use FFmpeg flag "-f mpeg" to target MPEG PS and "-f mpegts" to target MPEG TS.
:: For example, if an input MPEG TS container has a ".mpg" extension, it can be processed via:
:: ffmpeg -y -i %THE_BACKUP_NAME% -c:v copy -c:a copy -f mpegts %THE_ORIGINAL_NAME%
:: This will result in a fixed MPEG TS container with a ".mpg" extension.

:: Look for media containers with the following extension  
SET CONTAINER_EXTN=mkv

:: Recursively search for required media containers
for /f "delims=" %%f in ('dir "*.%CONTAINER_EXTN%" /s/b') do @call:thefun "%%f"
goto:eof

:thefun
:: Resolving filenames
SET THE_ORIGINAL_NAME="%~f1"
SET THE_DRIVE_LETTER=%~d1
SET THE_PATH=%~p1
SET THE_FILE=%~n1
SET THE_EXTN=%~x1
SET THE_BACKUP_NAME="%THE_DRIVE_LETTER%%THE_PATH%%THE_FILE%original.%CONTAINER_EXTN%"
SET THE_BACKUP_NAME_NEW_EXTN=%THE_DRIVE_LETTER%%THE_PATH%%THE_FILE%.mp4

:: Backup the original video
move /Y %THE_ORIGINAL_NAME% %THE_BACKUP_NAME%
:: Process with FFMPEG
ffmpeg -y -i %THE_BACKUP_NAME% -crf 26 %THE_BACKUP_NAME_NEW_EXTN%
goto:eof